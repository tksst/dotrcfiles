# このzshrcをOS付属のzshrcからsourceで読み込むと良いと思います。

iscygwin(){
    [[ $OSTYPE == cygwin ]] && return 0
        return 1
}

autoload -U compinit
compinit

HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=40000

setopt hist_ignore_dups     # ignore duplication command history list
setopt share_history        # share command history data
setopt correct
setopt auto_pushd

# remove command lines from the history list when the first character on the
# line is a space
setopt histignorespace

# display PID when suspending processes as well
setopt longlistjobs

# report the status of backgrounds jobs immediately
setopt notify

# whenever a command completion is attempted, make sure the entire command path
# is hashed first.
setopt hash_list_all

# don't push the same dir twice.
setopt pushd_ignore_dups

bindkey -e

if [[ "$TERM" != emacs ]]; then
    [[ -z "$terminfo[kdch1]" ]] || bindkey -M emacs "$terminfo[kdch1]" delete-char
    [[ -z "$terminfo[kdch1]" ]] || bindkey -M vicmd "$terminfo[kdch1]" vi-delete-char
    [[ -z "$terminfo[khome]" ]] || bindkey -M emacs "$terminfo[khome]" beginning-of-line
    [[ -z "$terminfo[khome]" ]] || bindkey -M vicmd "$terminfo[khome]" vi-beginning-of-line
    [[ -z "$terminfo[kend]"  ]] || bindkey -M emacs "$terminfo[kend]"  end-of-line
    [[ -z "$terminfo[kend]"  ]] || bindkey -M vicmd "$terminfo[kend]"  vi-end-of-line
    [[ -z "$terminfo[kich1]" ]] || bindkey -M emacs "$terminfo[kich1]" overwrite-mode
    [[ -z "$terminfo[kich1]" ]] || bindkey -M vicmd "$terminfo[kich1]" overwrite-mode

    [[ -z "$terminfo[cuu1]"  ]] || bindkey -M viins "$terminfo[cuu1]"  vi-up-line-or-history
    [[ -z "$terminfo[cuf1]"  ]] || bindkey -M viins "$terminfo[cuf1]"  vi-forward-char
    [[ -z "$terminfo[kcuu1]" ]] || bindkey -M viins "$terminfo[kcuu1]" vi-up-line-or-history
    [[ -z "$terminfo[kcud1]" ]] || bindkey -M viins "$terminfo[kcud1]" vi-down-line-or-history
    [[ -z "$terminfo[kcuf1]" ]] || bindkey -M viins "$terminfo[kcuf1]" vi-forward-char
    [[ -z "$terminfo[kcub1]" ]] || bindkey -M viins "$terminfo[kcub1]" vi-backward-char

    #terminfoがあてにならない用に手動追加
    #delete
    bindkey -M vicmd "\e[3~" vi-delete-char
    bindkey -M emacs "\e[3~" delete-char
    #home
    bindkey -M emacs "\e[1~" beginning-of-line
    bindkey -M emacs "\e[H"  beginning-of-line
    #end
    bindkey -M emacs "\e[4~" end-of-line
    bindkey -M emacs "\e[F"  end-of-line
    #pageup
    bindkey -M emacs "\e[5~" beginning-of-buffer-or-history
    #pagedown
    bindkey -M emacs "\e[6~" end-of-buffer-or-history
    #up
    bindkey -M emacs "\e[A"  up-line-or-history
    #down
    bindkey -M emacs "\e[B"  down-line-or-history
    #ins
    bindkey -M emacs "\e[2~" overwrite-mode
    bindkey -M vicmd "\e[2~" overwrite-mode

fi


local p_date="[%F{cyan}%D{%m/%d %T}%f]"
local p_tty="[%F{green}%l%f]"
local p_rv="%(?,,[%B%F{red}%?%f%b])"
local p_cdir="%B%F{blue}[%~]%f%b"
local p_info="%F{yellow}%n@%m%f"
local p_mark="%(!,#,$)"
if iscygwin;then
    for g in $(id -G); do
        [ $g -eq 0 ] && p_mark="#"
    done
fi
PROMPT="$p_date$p_tty$p_rv$p_cdir"$'\n'"$p_info$p_mark "

case "${TERM}" in
kterm*|xterm*|putty)
    precmd() {
        echo -n "\e]0;${USER}@${HOST%%.*}:${PWD}\007"
    }
    ;;

screen)
    precmd() {
        echo -n "\e]0;${USER}@${HOST%%.*}:${PWD}\007"
        echo -n "\ek$(basename $(pwd))/\e\\"
    }
    preexec() {
        echo -n "\ek${1%% *}\e\\"
    }
    ;;
esac


if iscygwin;then
    alias ls="ls --color=auto"
fi
